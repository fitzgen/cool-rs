use crate::ast;
use crate::frontend::lexer;
use failure;

grammar<'input>(ctx: &mut ast::Context);

pub Program: Vec<ast::NodeId> = <Class+>;

SepList<T, S>: Vec<T> = {
    => vec![],
    <e:T> <v:(S <T>)*> => {
        let mut v = v;
        v.insert(0, e);
        v
    }
};

Type: ast::TypeIdentifier = <t:TypeToken> =>
    ctx.new_type_identifier(t);

Identifier: ast::Identifier = <i:IdentifierToken> =>
    ctx.new_identifier(i);

Class: ast::NodeId =
    "class" <name:Type> <parent:("inherits" <Type>)?> "{" <features:(<Feature> ";")*> "}" ";" => {
        ctx.new_node(ast::Node::Class {
            name,
            parent,
            features,
        })
    };

Feature: ast::NodeId = {
    <name:Identifier> "(" <formals:SepList<Formal, ",">> ")" ":" <ty:Type> "{" <expr:Expr<"yes">> "}" =>
        ctx.new_node(ast::Node::Method {
            name,
            formals,
            ty,
            expr,
        }),
    <name:Identifier> ":" <ty:Type> <expr:("<-" <Expr<"yes">>)?> =>
        ctx.new_node(ast::Node::Property { name, ty, expr }),
};

Formal: (ast::Identifier, ast::TypeIdentifier) = <i:Identifier> ":" <t:Type> => (i, t);

Expr<AllowLet>: ast::NodeId = {
    <NotExpr<AllowLet>>,
    <id:Identifier> "<-" <expr:Expr<AllowLet>> => ctx.new_node(ast::Node::Assign { id, expr }),
};

NotExpr<AllowLet>: ast::NodeId = {
    <RelExpr<AllowLet>>,
    "not" <expr:RelExpr<AllowLet>> => ctx.new_node(ast::Node::Not { expr }),
};

RelExpr<AllowLet>: ast::NodeId = {
    <ArithExpr<AllowLet>>,
    <lhs:ArithExpr<"no">> "<=" <rhs:ArithExpr<AllowLet>> => ctx.new_node(ast::Node::LessThanEqual { lhs, rhs }),
    <lhs:ArithExpr<"no">> "<" <rhs:ArithExpr<AllowLet>> => ctx.new_node(ast::Node::LessThan { lhs, rhs }),
    <lhs:ArithExpr<"no">> "=" <rhs:ArithExpr<AllowLet>> => ctx.new_node(ast::Node::Equal { lhs, rhs }),
};

ArithExpr<AllowLet>: ast::NodeId = {
    <ProdExpr<AllowLet>>,
    <lhs:ArithExpr<"no">> "+" <rhs:ProdExpr<AllowLet>> => ctx.new_node(ast::Node::Add { lhs, rhs }),
    <lhs:ArithExpr<"no">> "-" <rhs:ProdExpr<AllowLet>> => ctx.new_node(ast::Node::Sub { lhs, rhs }),
};

ProdExpr<AllowLet>: ast::NodeId = {
    <IsVoidExpr<AllowLet>>,
    <lhs:ProdExpr<"no">> "*" <rhs:IsVoidExpr<AllowLet>> => ctx.new_node(ast::Node::Mul { lhs, rhs }),
    <lhs:ProdExpr<"no">> "/" <rhs:IsVoidExpr<AllowLet>> => ctx.new_node(ast::Node::Div { lhs, rhs }),
};

IsVoidExpr<AllowLet>: ast::NodeId = {
    <NegExpr<AllowLet>>,
    "isvoid" <expr:NegExpr<AllowLet>> => ctx.new_node(ast::Node::IsVoid { expr }),
};

NegExpr<AllowLet>: ast::NodeId = {
    <DotExpr<AllowLet>>,
    "~" <expr:DotExpr<AllowLet>> => ctx.new_node(ast::Node::Negate { expr }),
};

DotExpr<AllowLet>: ast::NodeId = {
    <SimpleExpr<AllowLet>>,
    <receiver:DotExpr<"no">> <cast:("@" <Type>)?> "." <method:Identifier> "(" <args:SepList<Expr<"yes">, ",">> ")" =>
        ctx.new_node(ast::Node::Dispatch {
            receiver,
            cast,
            method,
            args,
        }),
};

SimpleExpr<AllowLet>: ast::NodeId = {
    <e:LetExpr> if AllowLet == "yes" => e,
    <method:Identifier> "(" <args:SepList<Expr<"yes">, ",">> ")" => {
        let id = ctx.new_identifier("self");
        let receiver = ctx.new_node(ast::Node::VariableReference { id });
        let cast = None;
        ctx.new_node(ast::Node::Dispatch { receiver, cast, method, args })
    },
    "if" <condition:Expr<"yes">> "then" <consequent:Expr<"yes">> "else" <alternative:Expr<"yes">> "fi" =>
        ctx.new_node(ast::Node::IfThenElse {
            condition,
            consequent,
            alternative,
        }),
    "while" <condition:Expr<"yes">> "loop" <body:Expr<"yes">> "pool" =>
        ctx.new_node(ast::Node::While { condition, body }),
    "{" <exprs:(<Expr<"yes">> ";")+> "}" =>
        ctx.new_node(ast::Node::Block { exprs }),
    "case" <expr:Expr<"yes">> "of" <cases:(<Identifier> ":" <Type> "=>" <Expr<"yes">> ";")+> "esac" =>
        ctx.new_node(ast::Node::Case { expr, cases }),
    "new" <ty:Type> =>
        ctx.new_node(ast::Node::New { ty }),
    "(" <e:Expr<"yes">> ")" =>
        e,
    <id:Identifier> =>
        ctx.new_node(ast::Node::VariableReference { id }),
    <i:IntegerToken> =>
        ctx.new_node(ast::Node::IntegerConst(i)),
    <s:StringToken> =>
        ctx.new_string_const(s),
    "true" =>
        ctx.new_node(ast::Node::BoolConst(true)),
    "false" =>
        ctx.new_node(ast::Node::BoolConst(false)),
};

LetExpr: ast::NodeId = {
    "let" <id:Identifier> ":" <ty:Type> <expr:("<-" <Expr<"yes">>)?>
        <bindings:("," <Identifier> ":" <Type> <("<-" <Expr<"yes">>)?> )*>
        "in" <body:Expr<"yes">> => {
            let body = bindings.into_iter().rev().fold(body, |body, (id, ty, expr)| {
                ctx.new_node(ast::Node::LetIn { id, ty, expr, body })
            });
            ctx.new_node(ast::Node::LetIn { id, ty, expr, body })
        }
};

extern {
    type Location = usize;
    type Error = failure::Error;

    enum lexer::Token<'input> {
        IntegerToken => lexer::Token::Integer(<i64>),
        IdentifierToken => lexer::Token::Identifier(<&'input str>),
        TypeToken => lexer::Token::Type(<&'input str>),
        StringToken => lexer::Token::String(<String>),

        // Keywords
        "class" => lexer::Token::Class,
        "else" => lexer::Token::Else,
        "false" => lexer::Token::False,
        "fi" => lexer::Token::Fi,
        "if" => lexer::Token::If,
        "in" => lexer::Token::In,
        "inherits" => lexer::Token::Inherits,
        "isvoid" => lexer::Token::Isvoid,
        "let" => lexer::Token::Let,
        "loop" => lexer::Token::Loop,
        "pool" => lexer::Token::Pool,
        "then" => lexer::Token::Then,
        "while" => lexer::Token::While,
        "case" => lexer::Token::Case,
        "esac" => lexer::Token::Esac,
        "new" => lexer::Token::New,
        "of" => lexer::Token::Of,
        "not" => lexer::Token::Not,
        "true" => lexer::Token::True,

        // Symbols
        "{" => lexer::Token::LeftCurly,
        "}" => lexer::Token::RightCurly,
        "(" => lexer::Token::LeftParen,
        ")" => lexer::Token::RightParen,
        ";" => lexer::Token::Semicolon,
        ":" => lexer::Token::Colon,
        "." => lexer::Token::Dot,
        "," => lexer::Token::Comma,
        "<-" => lexer::Token::Assign,
        "@" => lexer::Token::At,
        "+" => lexer::Token::Add,
        "-" => lexer::Token::Sub,
        "*" => lexer::Token::Mul,
        "/" => lexer::Token::Div,
        "~" => lexer::Token::Negate,
        "<" => lexer::Token::LessThan,
        "<=" => lexer::Token::LessThanEqual,
        "=" => lexer::Token::Equal,
        "=>" => lexer::Token::RightArrow,
    }
}
